buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        //classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}
//apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'org.cukesalad'
archivesBaseName = project.name
version = '1.0.2'
jar {
  manifest {
    attributes 'Implementation-Title': 'Gradle Quickstart',
    'Implementation-Version': version
  }
}

repositories { mavenCentral() }

dependencies {
  compile('info.cukes:cucumber-java:1.2.4')
  compile(group: 'junit', name: 'junit', version: '4.12')
  compile('info.cukes:cucumber-junit:1.2.4')
  compile('commons-io:commons-io:2.4')
  compile('ch.qos.logback:logback-classic:1.1.3')
  compile('com.fasterxml.jackson.core:jackson-databind:2.4.5')
  testCompile('org.hsqldb:hsqldb:2.3.3')
}
task wrapper(type: Wrapper) {
  gradleVersion = '2.9'
}


////////////////////////////////////////////////////
// this is needed for adding javadoc jar and source jar in distribution
task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}
task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
artifacts {
  archives javadocJar, sourcesJar
}
////////////////////////////////////////////////////


////////////////////////////////////////////////////
// this is needed for signing the jars in distribution
signing {
    sign configurations.archives
}
////////////////////////////////////////////////////


////////////////////////////////////////////////////
// this is needed for deploying the jars to maven central
uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name archivesBaseName
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'This module is intended to help in testing REST services.'
        url 'http://cukesalad.org'

        scm {
          connection 'scm:git:git@github.com:cukesalad/CukeDBSalad.git'
          developerConnection 'scm:git:git@github.com:cukesalad/CukeDBSalad.git'
          url 'https://github.com/cukesalad/CukeDBSalad'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'bharathcp'
            name 'Bharath Prakash'
            email 'cippy.bharath@gmail.com'
          }
        }
      }
    }
  }
}

////////////////////////////////////////////////
// This is needed for jfrog bintray plugin
/*pkg {
  version {
              name = '1.0-SNAPSHOT'
              desc = 'Cuke REST salad'
              released  = new Date()
      }
}*/
////////////////////////////////////////////////